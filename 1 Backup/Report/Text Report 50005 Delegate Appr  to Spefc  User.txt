OBJECT Report 50005 Delegate Appr. to Spefc. User
{
  OBJECT-PROPERTIES
  {
    Date=02/21/24;
    Time=[ 9:51:00 PM];
    Modified=Yes;
    Version List=;
  }
  PROPERTIES
  {
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 1   ;    ;DataItem;                    ;
               DataItemTable=Table454;
               DataItemTableView=SORTING(Entry No.)
                                 WHERE(Status=CONST(Open));
               OnPreDataItem=BEGIN
                               "Approval Entry".SETRANGE("Approver ID", ApprovalOf);
                               IF TableID > 0 THEN
                                 "Approval Entry".SETRANGE("Table ID", TableID);
                               SkipReportRecLoop := FALSE;

                               IF (TableID IN [DATABASE::"Purchase Header", DATABASE::"Sales Header"]) THEN
                                 SkipReportRecLoop := TRUE;
                             END;

               OnAfterGetRecord=VAR
                                  SalesHeader@1001 : Record 36;
                                  PurchaseHeader@1002 : Record 38;
                                  RecRef@1003 : RecordRef;
                                  ApprovalsMgmt@1000 : Codeunit 1535;
                                BEGIN
                                  Delegate := FALSE;
                                  IF SkipReportRecLoop THEN BEGIN
                                    RecRef.GET("Record ID to Approve");
                                    CASE "Table ID" OF
                                      DATABASE::"Sales Header":
                                        BEGIN
                                          RecRef.SETTABLE(SalesHeader);
                                          IF (SalesHeader."Document Type" = DocumentType) AND (SalesHeader."Request type" = SalesRequestType) THEN
                                            Delegate := TRUE;
                                        END;

                                      DATABASE::"Purchase Header":
                                        BEGIN
                                          RecRef.SETTABLE(PurchaseHeader);
                                          IF (PurchaseHeader."Document Type" = DocumentType) AND (PurchaseHeader."Request Type" = PurchaseRequestType ) THEN
                                            Delegate := TRUE;
                                        END;
                                      ELSE
                                        CurrReport.SKIP;
                                    END;
                                  END ELSE
                                    Delegate := TRUE;

                                  IF Delegate THEN BEGIN
                                    "Approver ID" := DelegateTo;
                                    "Approval Entry".MODIFY(TRUE);
                                    ApprovalsMgmt.OnDelegateApprovalRequest("Approval Entry");
                                  END;
                                END;
                                 }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnQueryClosePage=BEGIN
                         IF CloseAction = ACTION::Cancel THEN
                           EXIT;

                         IF ApprovalOf = '' THEN
                           ERROR('Delegate Approval Of must have a value. Current value is blank.');

                         IF DelegateTo = '' THEN
                           ERROR('Delegate Approval To must have a value. Current value is blank.');

                         IF ApprovalOf = DelegateTo THEN
                           ERROR('Delegate Approval Of must not be equal to Delegate Approval To.');
                       END;

    }
    CONTROLS
    {
      { 1   ;    ;Container ;
                  ContainerType=ContentArea }

      { 2   ;1   ;Group     ;
                  Name=General;
                  GroupType=Group }

      { 4   ;2   ;Field     ;
                  CaptionML=ENU=Delegate Approval Of;
                  SourceExpr=ApprovalOf;
                  TableRelation="User Setup";
                  ShowMandatory=TRUE }

      { 3   ;2   ;Field     ;
                  CaptionML=ENU=Delegate Approval To;
                  SourceExpr=DelegateTo;
                  TableRelation="User Setup";
                  ShowMandatory=TRUE }

      { 5   ;2   ;Field     ;
                  Name=TableID;
                  CaptionML=ENU=Table Reference ID;
                  SourceExpr=TableID;
                  TableRelation=Object.ID WHERE (Type=CONST(Table));
                  OnValidate=BEGIN
                               CLEAR(SalesRequestType);
                               CLEAR(PurchaseRequestType);
                               CLEAR(DocumentType);
                             END;
                              }

      { 10  ;2   ;Field     ;
                  Name=DocumentType;
                  CaptionML=ENU=Document Type;
                  SourceExpr=DocumentType;
                  Editable=(TableID = 36) OR (TableID = 38);
                  OnValidate=BEGIN
                               IF DocumentType <> DocumentType::Invoice THEN BEGIN
                                 CLEAR(PurchaseRequestType);
                                 CLEAR(SalesRequestType);
                               END;
                             END;
                              }

      { 8   ;1   ;Group     ;
                  CaptionML=ENU=Sales Request Type;
                  GroupType=Group }

      { 7   ;2   ;Field     ;
                  Name=SalesRequestType;
                  CaptionML=ENU=Request Type;
                  SourceExpr=SalesRequestType;
                  Editable=(TableID = 36);
                  OnValidate=BEGIN
                               IF SalesRequestType <> SalesRequestType::" " THEN
                                 DocumentType := DocumentType::Invoice;
                             END;
                              }

      { 6   ;1   ;Group     ;
                  CaptionML=ENU=Purchase Request Type;
                  GroupType=Group }

      { 9   ;2   ;Field     ;
                  Name=PurchaseRequestType;
                  CaptionML=ENU=Request Type;
                  SourceExpr=PurchaseRequestType;
                  Editable=(TableID = 38);
                  OnValidate=BEGIN
                               IF PurchaseRequestType <> PurchaseRequestType::" " THEN
                                 DocumentType := DocumentType::Invoice;
                             END;
                              }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      ApprovalOf@1001 : Code[50];
      DelegateTo@1000 : Code[50];
      TableID@1002 : Integer;
      DocumentType@1009 : 'Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order';
      SalesRequestType@1003 : ' ,Tools & Equipment Requisition,Withdrawal,Return Merchandise Authorization,Request For Exception,Work Change Request';
      PurchaseRequestType@1004 : ' ,Cash Advance,Request for Payment,Liquidation,Reimbursement,Request for Purchase';
      ShowSalesRequestType@1005 : Boolean;
      ShowPurchaseRequestType@1006 : Boolean;
      SkipReportRecLoop@1007 : Boolean;
      Delegate@1008 : Boolean;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

