OBJECT Table 50901 Daily Time Record
{
  OBJECT-PROPERTIES
  {
    Date=08/16/18;
    Time=[ 3:24:51 PM];
    Modified=Yes;
    Version List=NAVOC1.00.02.11;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               CheckEmployeeTimeUserSetup;
             END;

    OnModify=BEGIN
               CheckEmployeeTimeUserSetup;
             END;

    OnDelete=BEGIN
               IF "JPS DRT ID" <> 0 THEN
                 ERROR(Txt010);
             END;

    OnRename=BEGIN
               IF "JPS DRT ID" <> 0 THEN
                 ERROR(Txt009);
             END;

  }
  FIELDS
  {
    { 10  ;   ;User ID             ;Code50        ;TableRelation="User Setup";
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 20  ;   ;Employee Code       ;Code20        ;TableRelation=Employee;
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 30  ;   ;Date                ;Date          ;NotBlank=Yes }
    { 40  ;   ;Time In             ;Time          ;TableRelation="Time Selector";
                                                   OnValidate=BEGIN
                                                                TESTFIELD("Time In");
                                                                IF ("Time Out" <> 0T) AND ("Time In" > "Time Out") THEN
                                                                  ERROR(Txt007, FIELDCAPTION("Time In"), FIELDCAPTION("Time Out"));
                                                              END;

                                                   ValidateTableRelation=No;
                                                   NotBlank=Yes }
    { 50  ;   ;Time Out            ;Time          ;TableRelation="Time Selector";
                                                   OnValidate=BEGIN
                                                                TESTFIELD("Time In");
                                                                IF ("Time In" <> 0T) AND ("Time Out" < "Time In") THEN
                                                                  ERROR(Txt008, FIELDCAPTION("Time Out"), FIELDCAPTION("Time In"));
                                                              END;

                                                   ValidateTableRelation=No }
    { 50500;  ;JPS DRT ID          ;Integer       ;Description=JPS Daily Time Record ID;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;User ID,Employee Code,Date              ;Clustered=Yes }
    {    ;Date                                     }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      EmployeeTimeUserSetup@1000 : Record 50970;
      Txt001@1005 : TextConst 'ENU=You''re not a valid user. Please contact your administrator.';
      Txt002@1004 : TextConst 'ENU=Do you want to proceed clocking in?';
      Txt003@1003 : TextConst 'ENU=Do you want to proceed clocking out?';
      Txt004@1002 : TextConst 'ENU=You''re not allowed to clock out twice.';
      Txt005@1001 : TextConst 'ENU=You do not have a clock in entry for the date of %1. Clock in first then clock out to proceed.';
      Txt006@1006 : TextConst 'ENU=You''re not allowed to clock out twice.';
      JPSTimeLog@1007 : Record 50803;
      Txt007@1008 : TextConst 'ENU=%1 must not be greater than %2.';
      Txt008@1009 : TextConst 'ENU=%1 must not be less than %2.';
      Txt009@1010 : TextConst 'ENU=You are not allowed to rename this record. It is already synched to the Jeonsoft Payroll Suite.';
      Txt010@1011 : TextConst 'ENU=You are not allowed to delete the record when it is already synched to the Jeonsoft Payroll Suite.';

    LOCAL PROCEDURE InitRecord@2();
    BEGIN
      TESTFIELD("User ID");
      TESTFIELD("Employee Code");
      TESTFIELD(Date);
      TESTFIELD("Time In");
    END;

    PROCEDURE InitValue@3();
    BEGIN
      CheckEmployeeTimeUserSetup;
      "User ID" := EmployeeTimeUserSetup."User ID";
      "Employee Code" := EmployeeTimeUserSetup."Employee Code";
      Date := TODAY;
    END;

    PROCEDURE ClockIn@7();
    VAR
      PageClockInOut@1000 : Page 51203;
      Decision@1001 : Boolean;
    BEGIN
       CheckEmployeeTimeUserSetup;

       INIT;
       "User ID" := EmployeeTimeUserSetup."User ID";
       "Employee Code" := EmployeeTimeUserSetup."Employee Code";
       Date := TODAY;
       "Time In" := TIME;

        CLEAR(PageClockInOut);
        PageClockInOut.SetClockDetails(0, "Employee Code", Date, "Time In");
        Decision := PageClockInOut.RUNMODAL = ACTION::Yes;
        IF NOT Decision THEN
          EXIT;
        PageClockInOut.GetClockDetails("Time In");
        TESTFIELD("Time In");
       INSERT(TRUE);

       // Automatic Synchronize Daily Time Record to JPS TimeLog
       JPSTimeLog.InsertTimeIn(Rec);
    END;

    PROCEDURE ClockOut@19();
    VAR
      ClockInEntry@1000 : Record 50901;
      JPSSetup@1003 : Record 50900;
      JPSDailyTimeRecord@1004 : Record 50800;
      JPSEmployee@1005 : Record 50006;
      PageClockInOut@1002 : Page 51203;
      Decision@1001 : Boolean;
    BEGIN
       CheckEmployeeTimeUserSetup;
       ClockInEntry.SETRANGE("User ID", USERID);
       ClockInEntry.SETRANGE(Date, TODAY);
       IF ClockInEntry.FINDFIRST THEN BEGIN
         IF ClockInEntry."Time Out" <> 0T THEN
           ERROR(Txt006);
         ClockInEntry."Time Out" := TIME;
         CLEAR(PageClockInOut);
         PageClockInOut.SetClockDetails(1, ClockInEntry."Employee Code", ClockInEntry.Date, ClockInEntry."Time Out");
         Decision := PageClockInOut.RUNMODAL = ACTION::Yes;
         IF NOT Decision THEN
           EXIT;
         PageClockInOut.GetClockDetails(ClockInEntry."Time Out");
         ClockInEntry.TESTFIELD("Time Out");
         IF ClockInEntry.MODIFY(TRUE) THEN BEGIN
           // Automatic Synchronize Daily Time Record to JPS TimeLog
           IF JPSTimeLog.InsertTimeOut(ClockInEntry) THEN;
           JPSDailyTimeRecord.InsertRecFromDailyTimeRecord(ClockInEntry);
         END;

       END ELSE
        ERROR(Txt005, TODAY);
    END;

    LOCAL PROCEDURE CheckEmployeeTimeUserSetup@4();
    BEGIN
       EmployeeTimeUserSetup.SETRANGE("User ID", USERID);
       IF NOT EmployeeTimeUserSetup.FINDFIRST THEN
        ERROR(Txt001);
    END;

    PROCEDURE SetMyDailyTimeRecordFilter@1();
    BEGIN
      EmployeeTimeUserSetup.SETRANGE("User ID", USERID);
      IF NOT EmployeeTimeUserSetup.FINDFIRST THEN BEGIN
        FILTERGROUP(2);
        SETRANGE("User ID", '''''');
        FILTERGROUP(0);
      END ELSE BEGIN
        FILTERGROUP(2);
        SETRANGE("Employee Code", EmployeeTimeUserSetup."Employee Code");
        FILTERGROUP(0);
      END;
    END;

    BEGIN
    END.
  }
}

