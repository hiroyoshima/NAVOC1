OBJECT Table 9500 Email Item
{
  OBJECT-PROPERTIES
  {
    Date=11/25/16;
    Time=12:00:00 PM;
    Modified=Yes;
    Version List=NAVW19.00.00.47444,MAIL01;
  }
  PROPERTIES
  {
    CaptionML=ENU=Email Item;
  }
  FIELDS
  {
    { 1   ;   ;ID                  ;GUID          ;CaptionML=ENU=ID }
    { 2   ;   ;From Name           ;Text100       ;CaptionML=ENU=From Name }
    { 3   ;   ;From Address        ;Text250       ;OnValidate=VAR
                                                                MailManagement@1000 : Codeunit 9520;
                                                              BEGIN
                                                                IF "From Address" <> '' THEN
                                                                  MailManagement.CheckValidEmailAddresses("From Address");
                                                              END;

                                                   CaptionML=ENU=From Address }
    { 4   ;   ;Send to             ;Text250       ;OnValidate=BEGIN
                                                                IF "Send to" <> '' THEN
                                                                  CorrectAndValidateEmailList("Send to");
                                                              END;

                                                   CaptionML=ENU=Send to }
    { 5   ;   ;Send CC             ;Text250       ;OnValidate=BEGIN
                                                                IF "Send CC" <> '' THEN
                                                                  CorrectAndValidateEmailList("Send CC");
                                                              END;

                                                   CaptionML=ENU=Send CC }
    { 6   ;   ;Send BCC            ;Text250       ;OnValidate=BEGIN
                                                                IF "Send BCC" <> '' THEN
                                                                  CorrectAndValidateEmailList("Send BCC");
                                                              END;

                                                   CaptionML=ENU=Send BCC }
    { 7   ;   ;Subject             ;Text250       ;CaptionML=ENU=Subject }
    { 8   ;   ;Body                ;BLOB          ;CaptionML=ENU=Body }
    { 9   ;   ;Attachment File Path;Text250       ;CaptionML=ENU=Attachment File Path }
    { 10  ;   ;Attachment Name     ;Text250       ;CaptionML=ENU=Attachment Name }
    { 11  ;   ;Plaintext Formatted ;Boolean       ;CaptionML=ENU=Plaintext Formatted }
  }
  KEYS
  {
    {    ;ID                                      ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE Init@1();
    BEGIN
      ID := CREATEGUID;
    END;

    PROCEDURE Send@2(HideMailDialog@1000 : Boolean);
    VAR
      MailManagement@1001 : Codeunit 9520;
    BEGIN
      MailManagement.SendMailOrDownload(Rec,HideMailDialog);
    END;

    PROCEDURE SetBodyText@5(Value@1000 : Text);
    VAR
      DataStream@1001 : OutStream;
      BodyText@1002 : BigText;
    BEGIN
      CLEAR(Body);
      BodyText.ADDTEXT(Value);
      Body.CREATEOUTSTREAM(DataStream,TEXTENCODING::UTF8);
      BodyText.WRITE(DataStream);
    END;

    PROCEDURE GetBodyText@3() Value : Text;
    VAR
      DataStream@1000 : InStream;
      BodyText@1001 : BigText;
      BodyText2@1002 : Text;
      BinaryReader@1004 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.IO.BinaryReader";
      Encoding@1003 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Text.Encoding";
    BEGIN
      // Note this is intended only to get the body in memory - not from the database.
      Value := '';
      //CALCFIELDS(Body);
      IF Body.HASVALUE THEN BEGIN
        Body.CREATEINSTREAM(DataStream,TEXTENCODING::UTF8);
        BodyText.READ(DataStream);
        BodyText.GETSUBTEXT(Value,1);
      END;
      EXIT(Value);
    END;

    LOCAL PROCEDURE CorrectAndValidateEmailList@6(VAR EmailAddresses@1000 : Text[250]);
    VAR
      MailManagement@1001 : Codeunit 9520;
    BEGIN
      EmailAddresses := CONVERTSTR(EmailAddresses,',',';');
      EmailAddresses := DELCHR(EmailAddresses,'<>');
      MailManagement.CheckValidEmailAddresses(EmailAddresses);
    END;

    BEGIN
    END.
  }
}

