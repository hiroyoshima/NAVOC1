OBJECT Codeunit 50586 Custom Timesheet Management
{
  OBJECT-PROPERTIES
  {
    Date=08/16/18;
    Time=11:02:51 AM;
    Modified=Yes;
    Version List=NAVOC1.00.02;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      StrmenuTxt01@1004 : TextConst 'ENU=Delete time sheet, Delete time sheet and disable resource to use a time sheet';
      StrmenuInstruction001@1005 : TextConst 'ENU=You''re about to delete the time sheet records.';
      ConfirmTxt01@1000 : TextConst 'ENU=Do you want to delete the selected time sheet?';
      ConfigProgressBar@1001 : Codeunit 8615;
      ConfigInitTxt@1002 : TextConst 'ENU=Deletion of Time Sheet';
      ConfigUpdateTxt@1003 : TextConst 'ENU=Deleting record...%1/%2';

    PROCEDURE DeleteTimeSheets@7(VAR TSH@1000 : Record 950);
    VAR
      Resource@1003 : Record 156;
      TotalDeletedTimeSheets@1002 : Integer;
      TotalTimeSheets@1001 : Integer;
      ConfirmDecision@1004 : Integer;
    BEGIN
       WITH TSH DO BEGIN
        ConfirmDecision := STRMENU(StrmenuTxt01,2,StrmenuInstruction001);
        IF ConfirmDecision = 0 THEN
          EXIT;
        TotalTimeSheets := TSH.COUNT;
        ConfigProgressBar.Init(TotalTimeSheets, 1, ConfigInitTxt);
        IF FIND('-') THEN BEGIN
          REPEAT
            IF Resource.GET(TSH."Resource No.") THEN BEGIN
              IF Resource.Blocked THEN BEGIN
                UnblockedResource(Resource);
                IF DeleteRecord(TSH) THEN;
                BlockResource(Resource);
              END ELSE
                DeleteRecord(TSH);
              IF ConfirmDecision = 2 THEN
                DisableResourceUseTimeSheet(Resource);
            END;
            TotalDeletedTimeSheets += 1;
            ConfigProgressBar.Update(STRSUBSTNO(ConfigUpdateTxt, TotalDeletedTimeSheets, TotalTimeSheets));
          UNTIL NEXT = 0;
          ConfigProgressBar.Close;
        END;
       END;
    END;

    [TryFunction]
    LOCAL PROCEDURE DeleteRecord@1(TimeSheetHeader@1000 : Record 950);
    BEGIN
       TimeSheetHeader.DELETE(TRUE);
    END;

    LOCAL PROCEDURE UnblockedResource@4(VAR Resource@1000 : Record 156);
    BEGIN
      Resource.VALIDATE(Blocked, FALSE);
      Resource.MODIFY(TRUE);
    END;

    LOCAL PROCEDURE BlockResource@5(VAR Resource@1000 : Record 156);
    BEGIN
      Resource.VALIDATE(Blocked, TRUE);
      Resource.MODIFY(TRUE);
    END;

    LOCAL PROCEDURE DisableResourceUseTimeSheet@50(VAR Resource@1000 : Record 156);
    VAR
      TimeSheetHeader@1001 : Record 950;
    BEGIN
      TimeSheetHeader.SETRANGE("Resource No.", Resource."No.");
      IF TimeSheetHeader.FINDFIRST THEN
        EXIT;
      Resource.VALIDATE("Use Time Sheet", FALSE);
      Resource.MODIFY(TRUE);
    END;

    BEGIN
    END.
  }
}

