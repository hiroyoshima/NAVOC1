OBJECT Table 5071 Campaign
{
  OBJECT-PROPERTIES
  {
    Date=08/17/18;
    Time=[ 4:16:34 PM];
    Modified=Yes;
    Version List=NAVW19.00.00.49968,NAVOC1.00.03;
  }
  PROPERTIES
  {
    DataCaptionFields=No.,Description;
    OnInsert=VAR
               CheckDateFormula@1000 : DateFormula;
             BEGIN
               IF "No." = '' THEN BEGIN
                 RMSetup.GET;
                 RMSetup.TESTFIELD("Campaign Nos.");
                 NoSeriesMgt.InitSeries(RMSetup."Campaign Nos.",xRec."No. Series",0D,"No.","No. Series");
               END;

               DimMgt.UpdateDefaultDim(
                 DATABASE::Campaign,"No.",
                 "Global Dimension 1 Code","Global Dimension 2 Code");

               // PHP Start
               "Responsibility Center" := UserSetupMgt.GetCampaignFilter;
               IF UserSetup.GET(USERID) THEN
                 UserSetup.TESTFIELD("Salespers./Purch. Code")
               ELSE
                 UserSetup.TESTFIELD("User ID");

               VALIDATE("Salesperson Code", UserSetup."Salespers./Purch. Code");
               VALIDATE("Starting Date", TODAY);
               RMSetup.GET;
               IF RMSetup."Campaign Cycle Completion" = CheckDateFormula THEN
                 VALIDATE("Ending Date", TODAY)
               ELSE
                 VALIDATE("Ending Date", CALCDATE(RMSetup."Campaign Cycle Completion", TODAY));
               // PHP End
             END;

    OnModify=BEGIN
               "Last Date Modified" := TODAY;

               IF ("Starting Date" <> xRec."Starting Date") OR
                  ("Ending Date" <> xRec."Ending Date")
               THEN
                 UpdateDates;
             END;

    OnDelete=VAR
               SalesPrice@1001 : Record 7002;
               SalesLineDisc@1000 : Record 7004;
               AssignedIndustry@1002 : Record 50033;
             BEGIN
               DimMgt.DeleteDefaultDim(DATABASE::Campaign,"No.");

               RMCommentLine.SETRANGE("Table Name",RMCommentLine."Table Name"::Campaign);
               RMCommentLine.SETRANGE("No.","No.");
               RMCommentLine.DELETEALL;

               CampaignEntry.SETCURRENTKEY("Campaign No.");
               CampaignEntry.SETRANGE("Campaign No.","No.");
               CampaignEntry.DELETEALL;

               CampaignMgmt.DeactivateCampaign(Rec,FALSE);

               SalesPrice.SETRANGE("Sales Type",SalesPrice."Sales Type"::Campaign);
               SalesPrice.SETRANGE("Sales Code","No.");
               SalesPrice.DELETEALL;

               SalesLineDisc.SETRANGE("Sales Type",SalesLineDisc."Sales Type"::Campaign);
               SalesLineDisc.SETRANGE("Sales Code","No.");
               SalesLineDisc.DELETEALL;


               AssignedIndustry.SETRANGE("Campaign Code", "No.");
               AssignedIndustry.DELETEALL;
             END;

    OnRename=BEGIN
               "Last Date Modified" := TODAY;
             END;

    CaptionML=ENU=Campaign;
    LookupPageID=Page5087;
  }
  FIELDS
  {
    { 1   ;   ;No.                 ;Code20        ;OnValidate=BEGIN
                                                                IF "No." <> xRec."No." THEN BEGIN
                                                                  RMSetup.GET;
                                                                  NoSeriesMgt.TestManual(RMSetup."Campaign Nos.");
                                                                  "No. Series" := '';
                                                                END;
                                                              END;

                                                   CaptionML=ENU=No. }
    { 2   ;   ;Description         ;Text250       ;CaptionML=ENU=Description;
                                                   Description=From:50 to 250 }
    { 3   ;   ;Starting Date       ;Date          ;OnValidate=BEGIN
                                                                // PHP Start
                                                                IF WithToDos THEN
                                                                  TESTFIELD("Starting Date");
                                                                // PHP End

                                                                IF ("Starting Date" > "Ending Date") AND ("Ending Date" > 0D) THEN
                                                                  ERROR(Text000,FIELDCAPTION("Starting Date"),FIELDCAPTION("Ending Date"));
                                                              END;

                                                   CaptionML=ENU=Starting Date }
    { 4   ;   ;Ending Date         ;Date          ;OnValidate=BEGIN
                                                                // PHP Start
                                                                IF WithToDos THEN
                                                                  TESTFIELD("Ending Date");
                                                                // PHP End
                                                                IF ("Ending Date" < "Starting Date") AND ("Ending Date" > 0D) THEN
                                                                  ERROR(Text001,FIELDCAPTION("Ending Date"),FIELDCAPTION("Starting Date"));
                                                              END;

                                                   CaptionML=ENU=Ending Date }
    { 5   ;   ;Salesperson Code    ;Code20        ;TableRelation=Salesperson/Purchaser;
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Salesperson Name");
                                                              END;

                                                   CaptionML=ENU=Salesperson Code }
    { 6   ;   ;Comment             ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Rlshp. Mgt. Comment Line" WHERE (Table Name=CONST(Campaign),
                                                                                                       No.=FIELD(No.)));
                                                   CaptionML=ENU=Comment;
                                                   Editable=No }
    { 7   ;   ;Last Date Modified  ;Date          ;CaptionML=ENU=Last Date Modified;
                                                   Editable=No }
    { 8   ;   ;No. Series          ;Code10        ;TableRelation="No. Series";
                                                   CaptionML=ENU=No. Series;
                                                   Editable=No }
    { 9   ;   ;Global Dimension 1 Code;Code20     ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(1,"Global Dimension 1 Code");
                                                              END;

                                                   CaptionML=ENU=Global Dimension 1 Code;
                                                   CaptionClass='1,1,1' }
    { 10  ;   ;Global Dimension 2 Code;Code20     ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(2,"Global Dimension 2 Code");
                                                              END;

                                                   CaptionML=ENU=Global Dimension 2 Code;
                                                   CaptionClass='1,1,2' }
    { 11  ;   ;Status Code         ;Code10        ;TableRelation="Campaign Status";
                                                   OnValidate=VAR
                                                                CampaignStatus@1000 : Record 5073;
                                                              BEGIN
                                                                // PHP Start
                                                                IF "Status Code" = '' THEN
                                                                  IF CheckOpenToDos THEN
                                                                    ERROR(Text004, Rec.TABLECAPTION);

                                                                TESTFIELD("Vendor No.");
                                                                TESTFIELD(Description);
                                                                TESTFIELD("Cost Analyst Code");
                                                                IF CampaignStatus.GET("Status Code") THEN BEGIN
                                                                  IF CampaignStatus."Required NDA" THEN
                                                                    TESTFIELD("With NDA");
                                                                  IF CampaignStatus."Required LOA" THEN
                                                                    TESTFIELD("With LOA");
                                                                  IF CampaignStatus."Required Accreditation" THEN
                                                                    TESTFIELD("Accreditation Category");
                                                                  IF CampaignStatus."Required Cost Analyst" THEN
                                                                    TESTFIELD("Cost Analyst Code");
                                                                  END;
                                                                TESTFIELD("Starting Date");
                                                                TESTFIELD("Ending Date");
                                                                TESTFIELD("Salesperson Code");

                                                                IF CampaignStatus.GET("Status Code") THEN BEGIN
                                                                  IF CampaignStatus."Activity Code" <> '' THEN
                                                                    CreateToDo(CampaignStatus."Cancel To-dos",TRUE);
                                                                  IF (CampaignStatus."Activity Code" = '') AND CampaignStatus."Cancel To-dos" THEN
                                                                    IF CONFIRM(Text003, FALSE, Rec.TABLECAPTION) THEN
                                                                      CreateToDo(CampaignStatus."Cancel To-dos", FALSE);
                                                                END;

                                                                CALCFIELDS("Campaign Status");
                                                                // PHP End
                                                              END;

                                                   CaptionML=ENU=Status Code }
    { 12  ;   ;Target Contacts Contacted;Integer  ;FieldClass=FlowField;
                                                   CalcFormula=Count("Interaction Log Entry" WHERE (Campaign No.=FIELD(No.),
                                                                                                    Campaign Target=CONST(Yes),
                                                                                                    Canceled=CONST(No),
                                                                                                    Date=FIELD(Date Filter),
                                                                                                    Postponed=CONST(No)));
                                                   CaptionML=ENU=Target Contacts Contacted;
                                                   Editable=No }
    { 13  ;   ;Contacts Responded  ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Interaction Log Entry" WHERE (Campaign No.=FIELD(No.),
                                                                                                    Campaign Response=CONST(Yes),
                                                                                                    Canceled=CONST(No),
                                                                                                    Date=FIELD(Date Filter),
                                                                                                    Postponed=CONST(No)));
                                                   CaptionML=ENU=Contacts Responded;
                                                   Editable=No }
    { 14  ;   ;Duration (Min.)     ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Interaction Log Entry"."Duration (Min.)" WHERE (Campaign No.=FIELD(No.),
                                                                                                                    Canceled=CONST(No),
                                                                                                                    Date=FIELD(Date Filter),
                                                                                                                    Postponed=CONST(No)));
                                                   CaptionML=ENU=Duration (Min.);
                                                   DecimalPlaces=0:0;
                                                   Editable=No }
    { 16  ;   ;Cost (LCY)          ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Interaction Log Entry"."Cost (LCY)" WHERE (Campaign No.=FIELD(No.),
                                                                                                               Canceled=CONST(No),
                                                                                                               Date=FIELD(Date Filter),
                                                                                                               Postponed=CONST(No)));
                                                   CaptionML=ENU=Cost (LCY);
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 17  ;   ;No. of Opportunities;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Opportunity Entry" WHERE (Campaign No.=FIELD(No.),
                                                                                                Active=CONST(Yes)));
                                                   CaptionML=ENU=No. of Opportunities;
                                                   Editable=No }
    { 18  ;   ;Estimated Value (LCY);Decimal      ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Opportunity Entry"."Estimated Value (LCY)" WHERE (Campaign No.=FIELD(No.),
                                                                                                                      Active=CONST(Yes)));
                                                   CaptionML=ENU=Estimated Value (LCY);
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 19  ;   ;Calcd. Current Value (LCY);Decimal ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Opportunity Entry"."Calcd. Current Value (LCY)" WHERE (Campaign No.=FIELD(No.),
                                                                                                                           Active=CONST(Yes)));
                                                   CaptionML=ENU=Calcd. Current Value (LCY);
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 20  ;   ;Date Filter         ;Date          ;FieldClass=FlowFilter;
                                                   CaptionML=ENU=Date Filter }
    { 23  ;   ;Action Taken Filter ;Option        ;FieldClass=FlowFilter;
                                                   CaptionML=ENU=Action Taken Filter;
                                                   OptionCaptionML=ENU=" ,Next,Previous,Updated,Jumped,Won,Lost";
                                                   OptionString=[ ,Next,Previous,Updated,Jumped,Won,Lost] }
    { 24  ;   ;Sales Cycle Filter  ;Code10        ;FieldClass=FlowFilter;
                                                   TableRelation="Sales Cycle";
                                                   CaptionML=ENU=Sales Cycle Filter }
    { 25  ;   ;Sales Cycle Stage Filter;Integer   ;FieldClass=FlowFilter;
                                                   TableRelation="Sales Cycle Stage".Stage WHERE (Sales Cycle Code=FIELD(Sales Cycle Filter));
                                                   CaptionML=ENU=Sales Cycle Stage Filter }
    { 26  ;   ;Probability % Filter;Decimal       ;FieldClass=FlowFilter;
                                                   CaptionML=ENU=Probability % Filter;
                                                   DecimalPlaces=1:1;
                                                   MinValue=0;
                                                   MaxValue=100 }
    { 27  ;   ;Completed % Filter  ;Decimal       ;FieldClass=FlowFilter;
                                                   CaptionML=ENU=Completed % Filter;
                                                   DecimalPlaces=1:1;
                                                   MinValue=0;
                                                   MaxValue=100 }
    { 28  ;   ;Contact Filter      ;Code20        ;FieldClass=FlowFilter;
                                                   TableRelation=Contact;
                                                   CaptionML=ENU=Contact Filter }
    { 29  ;   ;Contact Company Filter;Code20      ;FieldClass=FlowFilter;
                                                   TableRelation=Contact WHERE (Type=CONST(Company));
                                                   CaptionML=ENU=Contact Company Filter }
    { 30  ;   ;Estimated Value Filter;Decimal     ;FieldClass=FlowFilter;
                                                   CaptionML=ENU=Estimated Value Filter;
                                                   AutoFormatType=1 }
    { 31  ;   ;Calcd. Current Value Filter;Decimal;FieldClass=FlowFilter;
                                                   CaptionML=ENU=Calcd. Current Value Filter;
                                                   AutoFormatType=1 }
    { 32  ;   ;Chances of Success % Filter;Decimal;FieldClass=FlowFilter;
                                                   CaptionML=ENU=Chances of Success % Filter;
                                                   DecimalPlaces=0:0;
                                                   MinValue=0;
                                                   MaxValue=100 }
    { 33  ;   ;To-do Status Filter ;Option        ;FieldClass=FlowFilter;
                                                   CaptionML=ENU=To-do Status Filter;
                                                   OptionCaptionML=ENU=Not Started,In Progress,Completed,Waiting,Postponed;
                                                   OptionString=Not Started,In Progress,Completed,Waiting,Postponed }
    { 34  ;   ;To-do Closed Filter ;Boolean       ;FieldClass=FlowFilter;
                                                   CaptionML=ENU=To-do Closed Filter }
    { 35  ;   ;Priority Filter     ;Option        ;FieldClass=FlowFilter;
                                                   CaptionML=ENU=Priority Filter;
                                                   OptionCaptionML=ENU=Low,Normal,High;
                                                   OptionString=Low,Normal,High }
    { 36  ;   ;Team Filter         ;Code10        ;FieldClass=FlowFilter;
                                                   TableRelation=Team;
                                                   CaptionML=ENU=Team Filter }
    { 37  ;   ;Salesperson Filter  ;Code20        ;FieldClass=FlowFilter;
                                                   TableRelation=Salesperson/Purchaser;
                                                   CaptionML=ENU=Salesperson Filter }
    { 38  ;   ;Opportunity Entry Exists;Boolean   ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Opportunity Entry" WHERE (Campaign No.=FIELD(No.),
                                                                                                Active=CONST(Yes),
                                                                                                Salesperson Code=FIELD(Salesperson Filter),
                                                                                                Contact No.=FIELD(Contact Filter),
                                                                                                Contact Company No.=FIELD(Contact Company Filter),
                                                                                                Sales Cycle Code=FIELD(Sales Cycle Filter),
                                                                                                Sales Cycle Stage=FIELD(Sales Cycle Stage Filter),
                                                                                                Action Taken=FIELD(Action Taken Filter),
                                                                                                Estimated Value (LCY)=FIELD(Estimated Value Filter),
                                                                                                Calcd. Current Value (LCY)=FIELD(Calcd. Current Value Filter),
                                                                                                Completed %=FIELD(Completed % Filter),
                                                                                                Chances of Success %=FIELD(Chances of Success % Filter),
                                                                                                Probability %=FIELD(Probability % Filter),
                                                                                                Estimated Close Date=FIELD(Date Filter),
                                                                                                Close Opportunity Code=FIELD(Close Opportunity Filter)));
                                                   CaptionML=ENU=Opportunity Entry Exists;
                                                   Editable=No }
    { 39  ;   ;To-do Entry Exists  ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist(To-do WHERE (Campaign No.=FIELD(No.),
                                                                                  Contact No.=FIELD(Contact Filter),
                                                                                  Contact Company No.=FIELD(Contact Company Filter),
                                                                                  Salesperson Code=FIELD(Salesperson Filter),
                                                                                  Team Code=FIELD(Team Filter),
                                                                                  Status=FIELD(To-do Status Filter),
                                                                                  Closed=FIELD(To-do Closed Filter),
                                                                                  Priority=FIELD(Priority Filter),
                                                                                  Date=FIELD(Date Filter)));
                                                   CaptionML=ENU=To-do Entry Exists;
                                                   Editable=No }
    { 40  ;   ;Close Opportunity Filter;Code10    ;FieldClass=FlowFilter;
                                                   TableRelation="Close Opportunity Code";
                                                   CaptionML=ENU=Close Opportunity Filter }
    { 41  ;   ;Activated           ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Campaign Target Group" WHERE (Campaign No.=FIELD(No.)));
                                                   CaptionML=ENU=Activated;
                                                   Editable=No }
    { 50520;  ;Product Status      ;Option        ;OptionCaptionML=ENU=New Product,Existing Product;
                                                   OptionString=New Product,Existing Product;
                                                   Editable=No }
    { 50530;  ;Responsibility Center;Code10       ;TableRelation="Responsibility Center";
                                                   OnValidate=BEGIN
                                                                // PHP Start
                                                                 IF NOT UserSetupMgt.CheckRespCenter(3,"Responsibility Center") THEN
                                                                  ERROR(Text002, ResponsibilityCenter.TABLECAPTION, UserSetupMgt.GetCampaignFilter);
                                                                // PHP End
                                                              END;
                                                               }
    { 50540;  ;Campaign Status     ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Campaign Status".Description WHERE (Code=FIELD(Status Code)));
                                                   Editable=No }
    { 50550;  ;Incoming Document Entry No.;Integer }
    { 50560;  ;Vendor No.          ;Code20        ;TableRelation=Vendor WHERE (Vendor Type=FILTER(<>Employee));
                                                   OnValidate=VAR
                                                                Vendor@1000 : Record 23;
                                                              BEGIN
                                                                // PHP Start
                                                                IF Vendor.GET("Vendor No.") THEN BEGIN
                                                                  VALIDATE("Vendor Name", Vendor.Name);
                                                                  VALIDATE("Vendor Name 2", Vendor."Name 2");
                                                                END ELSE BEGIN
                                                                  CLEAR("Vendor Name");
                                                                  CLEAR("Vendor Name 2");
                                                                  END;
                                                                // PHP End
                                                              END;
                                                               }
    { 50561;  ;Vendor Name         ;Text50        ;FieldClass=Normal;
                                                   OnValidate=BEGIN
                                                                TESTFIELD("Vendor No.");
                                                              END;
                                                               }
    { 50562;  ;Vendor Name 2       ;Text50        ;OnValidate=BEGIN
                                                                TESTFIELD("Vendor No.");
                                                              END;
                                                               }
    { 50600;  ;With NDA            ;Boolean        }
    { 50605;  ;With LOA            ;Boolean        }
    { 50606;  ;Accreditation Category;Option      ;OptionCaptionML=ENU=" ,On-going,Done,Not Required";
                                                   OptionString=[ ,On-going,Done,Not Required] }
    { 50607;  ;Remarks             ;Text250        }
    { 50608;  ;Department Code     ;Code20        ;TableRelation="User Group" }
    { 50609;  ;Department          ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("User Group".Name WHERE (Code=FIELD(Department Code)));
                                                   Editable=No }
    { 50610;  ;Salesperson Name    ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Salesperson/Purchaser.Name WHERE (Code=FIELD(Salesperson Code)));
                                                   Editable=No }
    { 50630;  ;Cost Analyst Code   ;Code20        ;TableRelation=Salesperson/Purchaser;
                                                   OnValidate=BEGIN
                                                                TESTFIELD("Status Code", '');
                                                              END;
                                                               }
    { 50635;  ;Cost Analyst        ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Salesperson/Purchaser.Name WHERE (Code=FIELD(Cost Analyst Code)));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Cost Analyst");
                                                              END;

                                                   Editable=No }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
    {    ;Salesperson Code                         }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;No.,Description,Starting Date,Ending Date,Status Code }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=%1 must be before %2.';
      Text001@1001 : TextConst 'ENU=%1 must be after %2.';
      RMSetup@1002 : Record 5079;
      Campaign@1003 : Record 5071;
      RMCommentLine@1004 : Record 5061;
      CampaignEntry@1005 : Record 5072;
      UserSetup@1009 : Record 91;
      ResponsibilityCenter@1012 : Record 5714;
      NoSeriesMgt@1006 : Codeunit 396;
      DimMgt@1007 : Codeunit 408;
      CampaignMgmt@1008 : Codeunit 7030;
      Text002@1010 : TextConst 'ENU=Your identification is set up to process from %1 %2 only.';
      UserSetupMgt@1011 : Codeunit 5700;
      Text003@1013 : TextConst 'ENU=Do want to cancel all To-Dos related to this %1?';
      Text004@1014 : TextConst 'ENU=You still have pending To-do related to this %1. You must complete all related To-do to complete the process.';

    PROCEDURE AssistEdit@2(OldCampaign@1000 : Record 5071) : Boolean;
    BEGIN
      WITH Campaign DO BEGIN
        Campaign := Rec;
        RMSetup.GET;
        RMSetup.TESTFIELD("Campaign Nos.");
        IF NoSeriesMgt.SelectSeries(RMSetup."Campaign Nos.",OldCampaign."No. Series","No. Series") THEN BEGIN
          RMSetup.GET;
          RMSetup.TESTFIELD("Campaign Nos.");
          NoSeriesMgt.SetSeries("No.");
          Rec := Campaign;
          EXIT(TRUE);
        END;
      END;
    END;

    LOCAL PROCEDURE ValidateShortcutDimCode@29(FieldNumber@1000 : Integer;VAR ShortcutDimCode@1001 : Code[20]);
    BEGIN
      DimMgt.ValidateDimValueCode(FieldNumber,ShortcutDimCode);
      DimMgt.SaveDefaultDim(DATABASE::Campaign,"No.",FieldNumber,ShortcutDimCode);
      MODIFY;
    END;

    LOCAL PROCEDURE UpdateDates@1();
    VAR
      SalesPrice@1002 : Record 7002;
      SalesLineDisc@1001 : Record 7004;
      SalesPrice2@1003 : Record 7002;
      SalesLineDisc2@1004 : Record 7004;
    BEGIN
      MODIFY;
      SalesPrice.SETRANGE("Sales Type",SalesPrice."Sales Type"::Campaign);
      SalesPrice.SETRANGE("Sales Code","No.");
      SalesPrice.LOCKTABLE;
      IF SalesPrice.FIND('-') THEN
        REPEAT
          SalesPrice2 := SalesPrice;
          SalesPrice.DELETE;
          SalesPrice2.VALIDATE("Starting Date","Starting Date");
          SalesPrice2.INSERT(TRUE);
          SalesPrice2.VALIDATE("Ending Date","Ending Date");
          SalesPrice2.MODIFY;
        UNTIL SalesPrice.NEXT = 0;

      SalesLineDisc.SETRANGE("Sales Type",SalesLineDisc."Sales Type"::Campaign);
      SalesLineDisc.SETRANGE("Sales Code","No.");
      SalesLineDisc.LOCKTABLE;
      IF SalesLineDisc.FIND('-') THEN
        REPEAT
          SalesLineDisc2 := SalesLineDisc;
          SalesLineDisc.DELETE;
          SalesLineDisc2.VALIDATE("Starting Date","Starting Date");
          SalesLineDisc2.INSERT(TRUE);
          SalesLineDisc2.VALIDATE("Ending Date","Ending Date");
          SalesLineDisc2.MODIFY;
        UNTIL SalesLineDisc.NEXT = 0;
    END;

    PROCEDURE CreateToDo@3(CancelOldToDo@1000 : Boolean;CreateNewToDo@1001 : Boolean);
    VAR
      CampaignStatus@1002 : Record 5073;
      ToDo@1003 : Record 5080;
      Opp@1004 : Record 5092;
      RMCommentLine@1005 : TEMPORARY Record 5061;
      AttendeeTemp@1008 : TEMPORARY Record 5199;
      TodoInteractionLanguageTemp@1007 : TEMPORARY Record 5196;
      AttachmentTemp@1006 : TEMPORARY Record 5062;
    BEGIN
      // PHP Start
      IF CancelOldToDo THEN
        ToDo.CancelOpenTodosFromCampaign("No."); // Create a Cancel Open Todos in Todo

      IF CheckOpenToDos THEN
        ERROR(Text004, Rec.TABLECAPTION);

      IF CreateNewToDo THEN
        IF CampaignStatus .GET("Status Code") THEN
          IF CampaignStatus."Activity Code" <> '' THEN BEGIN
      //      Opp.GET("Opportunity No.");
            ToDo."No." := '';
            ToDo."Campaign No." := "No.";
      //      ToDo."Segment No." := Opp."Segment No.";
            ToDo."Salesperson Code" := "Salesperson Code";
            ToDo.Status := ToDo.Status::"In Progress";
            IF CampaignStatus."Required Cost Analyst" THEN BEGIN
              ToDo."Cost Analyst Code" := "Cost Analyst Code";
              TESTFIELD("Cost Analyst Code");
            END;
      //      ToDo.VALIDATE("Contact No.","Contact No.");
      //      ToDo."Opportunity No." := "Opportunity No.";
      //      ToDo."Opportunity Entry No." := "Entry No.";
            ToDo.Date := TODAY;
            ToDo.Duration := 1440 * 1000 * 60;
            ToDo.InsertTodo(
              ToDo,RMCommentLine,AttendeeTemp,
              TodoInteractionLanguageTemp,AttachmentTemp,
              CampaignStatus."Activity Code",FALSE);
          END;
      // PHP End
    END;

    LOCAL PROCEDURE WithToDos@10() : Boolean;
    VAR
      ToDo@1000 : Record 5080;
    BEGIN
      // PHP Start
      ToDo.RESET;
      ToDo.SETRANGE("Campaign No.", "No.");
      IF ToDo.FINDSET THEN
        EXIT(TRUE);

      EXIT(FALSE);
      // PHP End
    END;

    LOCAL PROCEDURE CheckOpenToDos@4() : Boolean;
    VAR
      Todo@1000 : Record 5080;
    BEGIN
      // PHP Start
      Todo.RESET;
      Todo.SETRANGE("Campaign No.", "No.");
      Todo.SETRANGE(Closed, FALSE);
      IF Todo.FINDSET THEN
        EXIT(TRUE);

      EXIT(FALSE);
      // PHP End
    END;

    BEGIN
    {
      Extend the maximum character of description, from 50 to 250
    }
    END.
  }
}

