OBJECT Codeunit 50013 Silent Inc. Doc. File Upload
{
  OBJECT-PROPERTIES
  {
    Date=08/16/18;
    Time=[ 6:12:02 PM];
    Modified=Yes;
    Version List=;
  }
  PROPERTIES
  {
    OnRun=VAR
            ForeignAttachment@1003 : Record 50020;
            ForeignSubAttachment@1015 : Record 50020;
            IncomingDocumentAttachment@1001 : Record 133;
            IncomingDocumentSubAttachment@1013 : Record 133;
            MainIncomingDocumentAttachment@1004 : Record 133;
            ImportAttachmentIncDoc@1000 : Codeunit 134;
            IncomingDocumentNo@1006 : Integer;
          BEGIN
            // Get Main Attachment
            ForeignAttachmentTemp.DELETEALL;
            ForeignAttachment.SETRANGE("Is Uploaded", FALSE);
            IF ForeignAttachment.FINDSET THEN
              REPEAT
                ForeignAttachmentTemp.RESET;
                ForeignAttachmentTemp.SETRANGE("Table No.", ForeignAttachment."Table No.");
                ForeignAttachmentTemp.SETRANGE("Document No.", ForeignAttachment."Document No.");
                ForeignAttachmentTemp.SETRANGE("Document Type", ForeignAttachment."Document Type");
                IF NOT ForeignAttachmentTemp.FINDFIRST THEN BEGIN
                  ForeignAttachmentTemp.INIT;
                  ForeignAttachmentTemp := ForeignAttachment;
                  ForeignAttachmentTemp.INSERT;
                END;
              UNTIL ForeignAttachment.NEXT = 0;

            // Saving of main Attachment
            ForeignAttachmentTemp.RESET;
            IF ForeignAttachmentTemp.FINDSET THEN
              REPEAT

                ForeignAttachment.GET(ForeignAttachmentTemp."Entry No.");
                IncomingDocumentNo := ForeignAttachment.GetIncomingDocumentEntryNo;

                CLEAR(FileName);

                // Generate Filename.
                FileName := FORMAT(ConfigPath + ForeignAttachment.GetAttachmentName);

                IF (ForeignAttachment.GetIncomingDocumentEntryNo = 0) AND FILE.EXISTS(FileName) THEN BEGIN
                  // Save Main Attachment to Incoming Document
                  IncomingDocumentAttachment.INIT;
                  IF ImportAttachmentIncDoc.ImportAttachment(IncomingDocumentAttachment, FileName) THEN
                    ForeignAttachment.UpdateForeignAttachment(IncomingDocumentAttachment);
                END;

                // Save Supporting Attachment
                ForeignSubAttachment.RESET;
                ForeignSubAttachment.SETRANGE("Table No.", ForeignAttachmentTemp."Table No.");
                ForeignSubAttachment.SETRANGE("Document No.", ForeignAttachmentTemp."Document No.");
                ForeignSubAttachment.SETRANGE("Document Type", ForeignAttachmentTemp."Document Type");
                ForeignSubAttachment.SETRANGE("Is Uploaded", FALSE);

                // Set filter only if main attachment is not exists.
                IF IncomingDocumentNo = 0 THEN
                  ForeignSubAttachment.SETFILTER("Entry No.", '<>%1', ForeignAttachmentTemp."Entry No.");

                IF ForeignSubAttachment.FINDSET THEN
                  REPEAT
                    FileName := FORMAT(ConfigPath + ForeignSubAttachment.GetAttachmentName);
                    IF IncomingDocumentNo = 0 THEN
                      IncomingDocumentNo := IncomingDocumentAttachment."Incoming Document Entry No.";

                    IF FILE.EXISTS(FileName) THEN BEGIN
                      IncomingDocumentSubAttachment.INIT;
                      IncomingDocumentSubAttachment.SETRANGE("Incoming Document Entry No.", IncomingDocumentNo);
                      IF  ImportAttachmentIncDoc.ImportAttachment(IncomingDocumentSubAttachment, FileName) THEN
                        ForeignSubAttachment.UpdateForeignAttachment(IncomingDocumentSubAttachment);
                    END;

                  UNTIL ForeignSubAttachment.NEXT = 0;

              UNTIL ForeignAttachmentTemp.NEXT = 0;
          END;

  }
  CODE
  {
    VAR
      ConfigPath@1000 : TextConst 'ENU=C:\Sample Files for Upload\';
      ForeignAttachmentTemp@1002 : TEMPORARY Record 50020;
      MainRecordRef@1001 : RecordRef;
      FileName@1003 : Text;
      InStr@1005 : InStream;

    BEGIN
    END.
  }
}

